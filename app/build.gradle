apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'
apply plugin: 'androidx.navigation.safeargs.kotlin'

allOpen {
    annotation 'ss.wallpad.testing.OpenClass'
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "ss.wallpad"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "ss.wallpad.runner.WallPadTestRunner"
    }

    def keys = new Properties()
    rootProject.file('keys.properties').withInputStream { stream -> keys.load(stream) }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            buildConfigField 'String', 'SUBSCRIPTION_KEY', keys['bingDebugKey']
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'SUBSCRIPTION_KEY', keys['bingReleaseKey']
        }
    }

    sourceSets {
        def sharedTestDir = 'src/sharedTest/java'
        test.java.srcDirs += sharedTestDir
        androidTest.java.srcDirs += sharedTestDir
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

// kotlin android extensions @Parcelize support
androidExtensions {
    experimental = true
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0'
    implementation 'androidx.navigation:navigation-runtime-ktx:2.2.0-alpha01'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.0-alpha01'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.0-alpha01'

    implementation 'com.github.bumptech.glide:annotations:4.9.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    implementation 'com.google.dagger:dagger:2.23.2'
    kapt 'com.google.dagger:dagger-compiler:2.23.2'
    implementation 'com.google.dagger:dagger-android:2.23.2'
    implementation 'com.google.dagger:dagger-android-support:2.23.2'
    kapt 'com.google.dagger:dagger-android-processor:2.23.2'

    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.5.0'

    implementation 'com.squareup.moshi:moshi:1.8.0'

    debugImplementation 'androidx.fragment:fragment-testing:1.2.0-alpha02'
    debugImplementation 'androidx.test:core-ktx:1.2.0'

    testImplementation 'androidx.arch.core:core-testing:2.0.1'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'androidx.test:runner:1.2.0'
    testImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.27.0'
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'org.robolectric:annotations:4.3'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'org.mockito:mockito-core:2.27.0'
    androidTestImplementation 'org.mockito:mockito-android:2.27.0'
    androidTestImplementation 'org.robolectric:annotations:4.3'
}
